plugins {
  id 'java'
  id 'jacoco'
  id 'org.springframework.boot' version '2.6.3'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'com.github.sherter.google-java-format' version '0.9'
  id 'info.solidsoft.pitest' version '1.7.0'
}

group = 'com.ticketbeast'

apply from: rootProject.file('hooks.gradle')

repositories {
  mavenCentral()
  maven {
    url = uri("https://maven.pkg.github.com/montealegreluis/service-buses-spring-boot")
    credentials {
      username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
      password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
    }
  }
  maven {
    url = uri("https://maven.pkg.github.com/montealegreluis/service-buses-problem-spring-boot")
    credentials {
      username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
      password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
    }
  }
  maven {
    url = uri("https://maven.pkg.github.com/montealegreluis/ticketbeast")
    credentials {
      username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
      password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
    }
  }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'com.montealegreluis:service-buses-problem-spring-boot:1.0.0'
  implementation 'org.springdoc:springdoc-openapi-ui:1.6.6'
  implementation 'org.mapstruct:mapstruct:1.4.2.Final'
  implementation 'com.montealegreluis:service-buses-spring-boot:4.1.0'
  implementation 'com.montealegreluis:ticketbeast:2.0.0'

  compileOnly 'org.projectlombok:lombok:1.18.22'

  annotationProcessor 'org.projectlombok:lombok:1.18.22'
  annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

  runtimeOnly 'mysql:mysql-connector-java'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'com.atlassian.oai:swagger-request-validator-mockmvc:2.26.2'
  testImplementation 'com.h2database:h2:2.1.210'
  testImplementation testFixtures('com.montealegreluis:ticketbeast:2.0.0')
}

jacocoTestReport {
  reports {
    html.enabled true
    xml.enabled false
    csv.enabled false
  }
}

test {
  useJUnitPlatform()
  finalizedBy("jacocoTestReport")
}

pitest {
  threads = 4
  outputFormats = ['HTML']
  timestampedReports = false
  junit5PluginVersion = '0.15'
  coverageThreshold = 93
  mutationThreshold = 98
  excludedClasses = ['com.ticketbeast.ticketbeastapi.config.*', 'com.ticketbeast.ticketbeastapi.errorhandling.*']
}
